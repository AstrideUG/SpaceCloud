import java.text.SimpleDateFormat

buildscript {
    ext {
        kotlinVersion = '1.3.50'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'maven'

    group 'de.astride'
    version '0.1.0'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    configurations {
        // configuration that holds jars to include in the jar
        compile.extendsFrom jarLibs
    }

    dependencies {
        jarLibs 'com.github.Piinguiin:NetFrame:master-SNAPSHOT'
        jarLibs "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        jarLibs "org.jetbrains.kotlin:kotlin-reflect"
    }

    jar {
        zip64 = true
        from {
            configurations.jarLibs.collect { it.isDirectory() ? it : zipTree(it) }
        }
        manifest {
            attributes(
                    'Built-By': System.properties['user.name'],
                    'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                    'Build-Revision': project.version,
                    'Created-By': "Gradle ${gradle.gradleVersion}",
                    'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                    'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
            )
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

}

